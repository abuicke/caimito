apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'dev.gravitycode.caimito'
                artifactId = 'caimito'
                version = '2.0.2'

                // Specify the components to publish
                from components.release

                pom {
                    name = 'caimito'
                    description = 'A small library of helpful classes for working with Android'
                    url = 'https://github.com/abuicke/caimito'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'adambuicke'
                            name = 'Adam Buicke'
                            email = 'buickea@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/abuicke/caimito.git'
                        developerConnection = 'scm:git:ssh://github.com/abuicke/caimito.git'
                        url = 'https://github.com/abuicke/caimito'
                    }
                }
            }
        }

        // Repository configuration
        repositories {
            maven {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")

                credentials {
                    /**
                     * Regular username and password fails with gradle. Requires user token to work.
                     *
                     * NOTE: The user token may change frequently, so always
                     * check the most up to date user token has been applied.
                     * */
                    username = project.findProperty("ossrhUserTokenUsername")//project.findProperty("ossrhUsername")
                    password = project.findProperty("ossrhUserTokenPassword")//project.findProperty("ossrhPassword")
                }
            }
        }
    }

    // Signing configuration (if required)
    signing {
        sign publishing.publications.release
    }
}